grammar dk.sdu.mdsd.ArduinoDSL with org.eclipse.xtext.common.Terminals

generate arduinoDSL "http://www.sdu.dk/mdsd/ArduinoDSL"
import "http://www.eclipse.org/emf/2002/Ecore" as ecore

Program: program+= (Rule | Node)*;

Rule:
    type=('when' | 'once') condition=Condition '{' body=RuleBody '}'
;


Condition:
    left=Exp operator=BOOLEAN_OPERATOR right=Exp;

Exp: ExpWeakOperator;

ExpWeakOperator returns Exp:
	ExpStrongOperator
	(
		({ExpAdd.left=current}"+"right=ExpStrongOperator)
		|
		({ExpSub.left=current}"-"right=ExpStrongOperator)
	)*
;

ExpStrongOperator returns Exp:
	ExpGroups
	(
		({ExpMul.left=current}"*"right=ExpGroups)
		|
		({ExpDiv.left=current}"/"right=ExpGroups)
		|
		({ExpMod.left=current}"%"right=ExpGroups)
	)*
;

ExpGroups returns Exp:
	Value
	|
	ExpParentheses
;

ExpParentheses:
	'(' sub=Exp ')'
;


terminal BOOLEAN_OPERATOR:
    '<' | '>' | '>=' | '<=' | '==' | '!='
;

Value:
     NumberLiteral | Attribute | Delta
;

Attribute:
    name=[Node]  component=[Component]
;

Delta:
    attr=Attribute 'delta'
;

NumberLiteral:
	float=DECIMAL | int=INT
;

terminal DECIMAL: INT '.' INT;

NUMBER returns ecore::EFloat: INT ('.' INT)?;

State:
    value=('on' | 'off')
;

RuleBody:
    assignment+= Assignment+
;

Assignment:
    attribute=Attribute "=" value=(Exp | State)
;

Node:
    name=ID '{' components+=Component+ '}'
;

Component:
    name=ID properties=ComponentBody
;

ComponentBody:
    io=IO type=TYPE 'pin' pin=INT rate=Rate? map=Map? smoothing=Smoothing?
;

terminal IO:
    'input' | 'output'
;

terminal TYPE:
    'analog' | 'digital'
;

Map:
    'map' in=Range '=>' out=Range
;

Range:
    low=NUMBER ':' high=NUMBER
;

Rate:
    'rate' value=INT
;

Smoothing:
    'smoothing' value=NUMBER
;